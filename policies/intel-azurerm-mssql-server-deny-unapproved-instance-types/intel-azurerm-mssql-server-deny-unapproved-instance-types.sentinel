import "tfplan/v2" as tfplan

param valid_actions default [
	["no-op"],
	["create"],
	["update"],
]

format = {
	"reset": "\033[0m",
	"info":  "\033[32m",
	"warn":  "\033[33m",
	"error": "\033[31m",
}

summary = func(input, level) {
	result = false
	if input.violations is empty {
		return true
	} else {
		log(input, format[level])
	}
	return result
}

log = func(input, output) {
	header(input, output)
	violations(input, output)
	return null
}

header = func(input, output) {
	print(
		output + "\t========================================================================\n",
		output + "\t                    _       _       _ \n",
		output + "\t                   (_)     | |     | |\n",
		output + "\t                    _ _ __ | |_ ___| |\n",
		output + "\t                   | | '_ \\| __/ _ \\ |\n",
		output + "\t                   | | | | | ||  __/ |\n",
		output + "\t                   |_|_| |_|\\__\\___|_|\n",
		output + "\t\n",
		output + "\t========================================================================\n",
		output + "\tName        :" + output + " " + "intel-azurerm-mssql-server-deny-unapproved-instance-types.sentinel\n",
		output + "\tCategory    :" + output + " " + "Infrastructure (IaaS)\n",
		output + "\tProvider    :" + output + " " + "hashicorp/azurerm\n",
		output + "\tResource    :" + output + " " + "azurerm_mssql_server\n",
		output + "\tParameter   :" + output + " " + "sku_name\n",
		output + "\tCheck       :" + output + " " + "sku_name does not contain\n",
		output + "\t\n",
		output + "\tCompute Optimized:\n",
		output + "\t              GP_Fsv2_8, GP_Fsv2_10\n",
		output + "\t              GP_Fsv2_12, GP_Fsv2_14\n",
		output + "\t              GP_Fsv2_16, GP_Fsv2_18\n",
		output + "\t              GP_Fsv2_20, GP_Fsv2_24\n",
		output + "\t              GP_Fsv2_32, GP_Fsv2_36\n",
		output + "\t              GP_Fsv2_72,\n",
	)
	return null
}

violations = func(input, output) {
	print(
		output + "\t========================================================================\n",
		output + "\tRESOURCE VIOLATIONS\n" + format.reset,
		output + "\tThe configured server type should use an Intel Xeon 3rd Generation Scalable processor (code-named Ice Lake)\n",
		output + "\t========================================================================\t",
	)
	for input.violations as violation {
		print(
			"\t",
			output + "name       :" + output + " " + violation.name + "\n\t",
			output + "type       :" + output + " " + violation.type + "\n\t",
			output + "address    :" + output + " " + violation.address + "\n\t",
			output + "message    :" + output + " " + violation.message + "\t\n",
			output + "\t------------------------------------------------------------------------\t",
		)
	}
	if (input.violations is not empty) {
		print(
			output + "\t",
			output + "Resources out of compliance: " + string(length(input.violations)) + "\n" + format.reset,
			output + "\t------------------------------------------------------------------------\t",
		)
	}
	return null
}

// Allowed server resource types
allowed_server_types = ["GP_Fsv2_8", "GP_Fsv2_10", "GP_Fsv2_12", "GP_Fsv2_14", "GP_Fsv2_16", "GP_Fsv2_18", "GP_Fsv2_20", "GP_Fsv2_24", "GP_Fsv2_32", "GP_Fsv2_36", "GP_Fsv2_72"]

doc = {
	"id":       "mssql01",
	"resource": "azurerm_mssql_server",
	"name":     "Server Type must be one of the approved instances",
}

// Filter resources by type
all_resources = filter tfplan.resource_changes as _, rc {
	rc.type is doc.resource and
		rc.mode is "managed" and
		rc.change.actions in valid_actions
}

// Filter resources that violate a given condition
violators = filter all_resources as _, r {
	r.change.after.sku_name not in allowed_server_types
}

// Build a summary report
summaryreport = {
	"id":   doc.id,
	"name": doc.name,
	"violations": map violators as _, violation {
		{
			"name":    violation.name,
			"address": violation.address,
			"type":    violation.type,
			"message": violation.change.after.sku_name + " is not an allowed server type.",
		}
	},
}

main = rule {
	summary(summaryreport, "warn")
}
